        
        program plot_spec_ascii

!       (C) Thorsten Tepper Garcia 2013
!
!       Generates a Gnuplot file and plots a SpecWizard spectrum and its
!       corresponding fit by reading from a series of ASCII files
!       <spectype>_z<redshift>_<ion>_fit_SN<s/n>.*
!       output by ifit and profles_hdf5
!       Following files are required:
!
!       *.fit: fit parameters
!       *.abs: min / max pixel of absorption regions; significane
!       *.isp: original spectrum (flux / noise vs. wavelength)
!       *_linepar.fit: line parameters
!       *_SynSpec.fit: model flux vs. wavelength
!
!       Modules

        use common_strings
        use common_nums
        use arrays_fit
        use detected_regions
        use spectrum
        use fit_quality
        use alloc

!       ------------------------------------------------------------------------
!       Variables declaration
!       ------------------------------------------------------------------------
        implicit none
        
        integer :: size_of_file
!        integer :: underscore_indx
        
        character(len=256) :: homedir
        character(len=3) :: ch_index, ch_tot_regs
        
        double precision :: gasdev

        logical :: file_exists
!       ------------------------------------------------------------------------
!       Define program error and warning messages
!       errmsg='ERROR: plot_spec_ascii ... '
!       warnmsg='WARNING: plot_spec_ascii ... '
!       statmsg='STATUS: plot_spec_ascii ... '

!       The lines below are meant to print the different
!       messages in different colors

!       In red=31
        errmsg=CHAR(27)//'[1;31mERROR (plot_spec_ascii): '//
     &  CHAR(27)//'[0m'
!       In yellow=33
        warnmsg=CHAR(27)//'[1;33mWARNING (plot_spec_ascii): '//
     &  CHAR(27)//'[0m'
!       In green=32
        statmsg=CHAR(27)//'[1;32mSTATUS (plot_spec_ascii): '//
     &  CHAR(27)//'[0m'

!       ------------------------------------------------------------------------
!       Define home directory
        
        call system('echo $HOME > homedir')
        open(10,file='homedir')
        read(10,'(a)') homedir
        close(10)
        call system('rm homedir')

!       ------------------------------------------------------------------------
!       Get arguments (specfile directory, common specfile prefix, output type)

        call read_input_params

!       ------------------------------------------------------------------------
!       Read header containing useful information about spectrum
!       check what parameters are important!

!       call read_header_short(file_id)
        spectype = 'short'
        simulation = 'DATA'
        spec_id = '1'
        noise_added = 'TRUE'

!       ------------------------------------------------------------------------
!       Start reading spectrum data

!       define path
        prefix = trim(specfile_dir)//'/'//trim(specfile_prefix)

        specfile= trim(prefix)//'.isp'          ! input to ifit by user
        regionsfile=trim(prefix)//'.abs'        ! created by ifit
        fitfile= trim(prefix)//'_model.fit'   ! created by ifit
        linesfile=trim(prefix)//'_lines.fit'  ! created by ifit

!       ------------------------------------------------------------------------
!       Reading *characteristic* fit parameters from file '<prefix>.fit'
!       generated by ifit

!       File exists?
        inquire(file=linesfile,exist=file_exists)

!       If file doesn't exist, stop cleanly
        if (.not.file_exists) then
         write(6,'(a)') trim(errmsg)//'File '//trim(linesfile)//
     &   ' does not exist'
         stop 1
        end if

!       Read header containing redshift, average optical depth, number of
!       identified lines, chi^2 value, and spectra shift from file
!       '<prefix>.fit'

        open(10,file=trim(linesfile), status='old')
         
         read(10,*) ! skip info lines
         read(10,*) 
         read(10,*) 
         read(10,*) 
         read(10,*) 
         read(10,*) 

         read(10,*) total_lines, average_sn, fwhm_kms, profile_str
         read(10,*) check_fit_str, chi_sq_reduced
         read(10,*) spectrum_shifted_str, spectrum_shift
         read(10,*) equiv_width_lim_mA, Nion_lim

        close(10)

!       remove underscores from ion string
!        underscore_indx=scan(ion,'_')
!        ion=ion(1:underscore_indx-1)//'-'//ion(underscore_indx+1:)

!       ------------------------------------------------------------------------
!       Check if lines were detected; if not, stop cleanly
        
        if (total_lines.le.0) then
         
         write(6,'(a)') trim(warnmsg)//
     &   'No lines detected in spectrum '//trim(specfile)//'!'  
         stop 1

        else
        
         write(6,'(a,i5)') trim(statmsg)//'ION: '//trim(ion)//
     &   ' Lines:', total_lines
        
         lines_detected_str = 'TRUE'
         lines_detected = .true.
        
        end if

!       ------------------------------------------------------------------------
!       Check if spectrum has been shifted and by which amount

        if (trim(spectrum_shifted_str).eq.'FALSE') then

         spectrum_shifted = .false.

        else if (trim(spectrum_shifted_str).eq.'TRUE') then
         
         spectrum_shifted = .true.

         write(6,'(a,i8,a)') trim(warnmsg)//'Spectrum shifted by ',
     &   spectrum_shift, ' pixel'

        end if
!       ------------------------------------------------------------------------
!       Read chi^2 value

        if (trim(check_fit_str).eq.'FALSE') then

         check_fit = .false.

         write(6,'(a,f5.2)') trim(statmsg)//
     &   'Fitted Spectrum with good chi^2 value', chi_sq_reduced

        else if (trim(check_fit_str).eq.'TRUE') then
         
         check_fit = .true.

         write(6,'(a,f5.2)') trim(warnmsg)//'Fitted Spectrum with possible bad '//
     &   'chi^2 value', chi_sq_reduced

        end if
!       ------------------------------------------------------------------------
!       Get dimensions of arrays
!       Should be the same for all!
         
!       determine size of spectrum; assumed to have no extra (e.g., comment)
!       lines

!       File exists?
         inquire(file=specfile,exist=file_exists)

!       If file doesn't exist, stop cleanly
         if (.not.file_exists) then
          write(6,*) trim(errmsg)//'File '//trim(specfile)//
     &    ' does not exist'
          stop 1
         end if

!       Define array dimension
        dim(1) = size_of_file(specfile)
        
        if (dim(1).gt.1) then
         nveloc = dim(1) !size of spectrum in pixel
        else
         write(6,'(a)') trim(errmsg)//'Bad spectrum size in file '//
     &   trim(specfile)
         stop 1
        end if
!       ------------------------------------------------------------------------
!       Allocate necessary memory for spectral quantities
        
        call allocate_spectrum_arrays(nveloc)

!       ----------------------------------------------------------------
!       read ascii fit file with wavelength, flux (fit), noise, spectrum
!       created by profiles_hdf5

        open(40,file=trim(fitfile))
        
          do j=1,nveloc  !loop over wl range
           read(40,*) wavelength(j), fit(j), sigma(j), flux(j)
          end do

        close(40)

!       ------------------------------------------------------------------------
!       EXTREMELY IMPORTANT:

!       re-define negative/zero noise values in order to avoid crash
        if (any(sigma.le.0.0d0)) then

         write(6,'(a)') trim(warnmsg)//
     &   'Negative/zero noise values in input spectrum!'
         write(6,'(a)') 'Will set these to 0.0d0'
         write(6,*)

        where(sigma.le.0.0d0) sigma = 0.0d0

        end if ! noise < 0 anywhere?

!       ------------------------------------------------------------------------
!       flux without noise is usually not available from data
        flux_nonoise(:) = 0.0d0

!        compute velocity along spectrum
!        the line below assumes l=l_0*exp(v/c) or (1+z)=exp(v/c)
        
        v_Hubble(1:nveloc) =
     &  CLIGHT*dlog(wavelength(1:nveloc)/wavelength(1))*1.0d-5 !km/s

!       ------------------------------------------------------------------------
!       Get line parameters, ordered by z_abs
        
        call allocate_fit_arrays(total_lines)
        
        open(50,file=trim(linesfile))

!       Equivalent width in mili-Angstroem
!       column densities given in log10, associated errors in linear scale!

         read(50,*) ! skip info lines
         read(50,*) 
         read(50,*) 
         read(50,*) 
         read(50,*) 
         read(50,*) 

!        skip first four lines (these were read in above)
         read(50,*)
         read(50,*)
         read(50,*)
         read(50,*)

         do i=1,total_lines
          read(50,*) ion, wl_central, z_abs(i), wl_line(i),
     &    log10_Nion(i), b_value(i),
     &    equiv_width_mA(i),
     &    v_line(i), dNion(i),
     &    db_value(i), dv_line(i)
         end do
        
        close(50)
!       ------------------------------------------------------------------------
!       Read detected regions (from file <prefix>.abs)

!       File exists?
         inquire(file=regionsfile,exist=file_exists)

!       If file doesn't exist, stop cleanly
         if (.not.file_exists) then
          write(6,*) trim(errmsg)//'File '//trim(regionsfile)//' does not exist'
          stop 1
         end if

         total_regions = size_of_file(regionsfile)

         call allocate_regions_arrays(total_regions)

!       Read detected region parameters
         open(10,file=trim(regionsfile), status='old')
         
          do i=1,total_regions
           read(10,*) dummy_int, pixmin(i), pixmax(i), wlmin(i), wlmax(i),
     &     velmin(i), velmax(i), significance(i)
          end do

         close(10)

!       ----------------------------------------------------------------
!       Determine number of lines per region;
!       Define velocity zero-point, i.e. find velocity of strongest line
!       in each region; the strength is given by the equivalent width
        
        v_central_region(:)=0.0d0
        lines_in_region(:) = 0

        do i=1,total_regions

         eqw_aux = 0.0d0

         do j=1,total_lines
          
           if ((v_line(j).ge.floor(velmin(i))).and.
     &     (v_line(j).le.ceiling(velmax(i)))) then
            
            if (equiv_width_mA(j).gt.eqw_aux) then
             
             eqw_aux = equiv_width_mA(j)
             v_central_region(i) = v_line(j)
             wl_central_region(i) = wl_line(j)
             lines_in_region(i) = lines_in_region(i) + 1

            end if
         
          end if

         end do
        end do
        
!       ------------------------------------------------------------------------
!       Check if there are regions with no lines
        skip_regions = count(lines_in_region.eq.0)
        if (skip_regions.gt.0)
     &   write(6,'(a,i4,a)') trim(warnmsg)//'Skipping ', skip_regions,
     &   ' regions with no lines!'

!       ------------------------------------------------------------------------
!       Check if there are zero values in dNion, since this quantity
!       is mostly used logarithmically
        
        if (any(dNion.eq.0.0d0)) then
         write(6,*) trim(warnmsg)//'Zero value in dNion encountered!'
         stop 1
        end if

!       ------------------------------------------------------------------------
!       FINISH READING DATA.
!       ------------------------------------------------------------------------

!       generate Gaussian random numbers to perform a statistical comparison of
!       residuals to intrinsic noise

!       seed to initialise Gaussian random number generator
        seed= 0
        gauss_random(:) = 0.0d0
        do i=1,nveloc
         gauss_random(i) = gasdev(seed)
        end do

!       ------------------------------------------------------------------------
!       OUTPUT
!       ------------------------------------------------------------------------

!       ------------------------------------------------------------------------
!       Writing out data file with blocks
!       ------------------------------------------------------------------------

!       Common data file
        
        infile=trim(specfile_prefix)//'.psh'

!       Remove data file if existent
        
        inquire(file='output_data/'//trim(infile),exist=file_exists)
        if (file_exists) call system('rm output_data/'//trim(infile))

        identifier='short'

!       Block 1: (wavel.,flux,fit,[v_Hubble],flux_nonoise,noise(random),sigma)
        call create_datafile_spec(infile,identifier,nveloc)

!       Block 2+3: line parameters: (in wavelength/v_Hubble, in redshift)
!       Avoid if no graphical output desired
        if (trim(out_type).ne.'none') then
         call create_datafile_lineparams(infile,identifier,total_lines)
        end if
!       N.B.: Output for blocks 2 is slightly different for 'term' and
!       'ps'. In the former, plus-minus sign is simply replaced by
!       brackets; also, column densities (and corresponding errors) are
!       given in logarithmic scale

!       Block 4: (residuals ,noise)
!       only for pixels within detected regions
        call create_datafile_residuals(infile)

!       ------------------------------------------------------------------------
!       Writing out Gnuplot file
!       ------------------------------------------------------------------------

!       ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        IF (trim(out_type).ne.'none') THEN

!       define string inset to distiguish plotfile of spectra/fit w/wo
!       instrumental convolution

        str_inset = ''
        if (fwhm_kms.gt.0.0d0) str_inset = '_conv'

!       ------------------------------------------------------------------------
!       X11 output

         if ((trim(out_type).eq.'term_wl').or.(trim(out_type).eq.
     &   'term_vel')) then

!       Write ONE gnuplot file per detected region; this loop starts
!       from i=0, where region = 0 means the full spectrum, and i=-1
!       plots the residuals, i.e. (fit - spectrum) and noise

          do indx=-2,total_regions

!       Convert region index to string

           write(ch_index,'(i3)') indx

!       Remove previous plotfile if existent

           plotfile='plotspec'//trim(str_inset)//'_'//trim(adjustl(ch_index))//
     &     '.plot'
          
           inquire(file='output_data/'//trim(plotfile),exist=file_exists)
           if (file_exists) call system('rm output_data/'//trim(plotfile))

!       Create gnuplot file (infile is data file)

           if (trim(out_type).eq.'term_wl') then
            call create_plotfile_term_wl(infile,spectype,indx,nveloc)
           else
!          call create_plotfile_term_vel(infile,spec_id,spectype,indx)
            write(6,*) trim(warnmsg)//'Output type term_vel not yet '//
     &      'available! Sorry...'
            stop 1
           end if

!       Compile with Gnuplot

           comstr='plotspec'//trim(str_inset)//'_'//trim(adjustl(ch_index))//'.plot'

!       N.B.: the gnuplot command-line options 'background' and 'xrm'
!       are used to override the default options, and result in x11
!       with black background and white text/border/axes

           call system("gnuplot -background black "//
     &     "-xrm 'gnuplot*borderColor:white' "//
     &     "./output_data/"//trim(comstr))
         
          end do
        
!       ------------------------------------------------------------------------
!       PostScript output

         else if ((trim(out_type).eq.'ps_wl').or.(trim(out_type).eq.
     &   'ps_vel')) then

!       'ps_vel' not yet available for long spectra
         if (trim(spectype).eq.'long'.and.trim(out_type).eq.'ps_vel') then
          write(6,*) trim(warnmsg)//trim(out_type)//' not yet available'
     &    //' for '//trim(spectype)//' spectra'
          stop 1
         end if
 
!       Remove previous plotfile if existent

         comstr='plotspec'//trim(str_inset)//'.plot'

         inquire(file='output_data/'//trim(comstr),exist=file_exists)
         if (file_exists) call system('rm output_data/'//trim(comstr))

!       Remove previous psfile if existent
!       NEW: include S/N in file name
          
          comstr='plotspec'//trim(str_inset)//'.ps'

          inquire(file='output_data/'//trim(comstr),exist=file_exists)
          if (file_exists) call system('rm output_data/'//trim(comstr))

!       Write ONE gnuplot file per detected region; this loop starts
!       from i=0, where region = 0 means the full spectrum, and i=-1
!       plots the residuals, i.e. (fit - spectrum) and noise

          do indx=-2,total_regions

!       Convert region index to string

           write(ch_index,'(i3)') indx

!       For outtype=ps, create only ONE gnuplot file with different
!       blocks, in order to plot spectra in a single document with
!       several pages

           if (trim(out_type).eq.'ps_wl') then
            call create_plotfile_ps_wl(infile,spectype,indx,nveloc)
           else
            call create_plotfile_ps_vel(infile,spectype,indx,nveloc)
           end if

          end do

!       Compile gnuplot file and open corresponding ps-file
         
          comstr='plotspec'//trim(str_inset)//'.plot'
          
          write(6,'(a)') trim(statmsg)//'gnuplot ./output_data/'//trim(comstr)
          call system('gnuplot ./output_data/'//trim(comstr))
          call system('gv -orientation=landscape ./output_data/plotspec'//trim(str_inset)//'.ps &')

!       Write temporal ps-file leaving out the first two pages, which
!       contain the residuals and the full spectrum, respectively
!       NEW: include S/N in file name

          comstr='psselect -e -o -p3- ./output_data/plotspec'//trim(str_inset)//'.ps '//
     &    './output_data/temp.ps'
         
          write(6,'(a)') trim(statmsg)//trim(comstr)
          call system(trim(comstr))

          write(ch_tot_regs,'(i3)') total_regions
         
!       Merge all pages (as panels) into a one-single page file and open
!       corresponding ps-file
!       A maximum number of 9 panels per pages seems to be a nice layout,
!       hence, handle following cases:
!       NEW: include S/N in file name

!       Case: regions < 9
        
          if (total_regions.le.9) then

!       Case: regions = 5 -> needs special handling since psnup fails at
!       finding an acceptable layout for this
           if (total_regions.eq.5) then

            comstr='psnup -l -m25 -n 6 ./output_data/temp.ps '
     &      //'./output_data/plotspec'//trim(str_inset)//'_thumbs.ps'

!       Case: regions = 7 -> IDEM
           else if (total_regions.eq.7) then

            comstr='psnup -l -m25 -n 8 ./output_data/temp.ps '
     &      //'./output_data/plotspec'//trim(str_inset)//'_thumbs.ps'

           else
        
            comstr='psnup -l -m25 -n '//trim(adjustl(ch_tot_regs))//
     &      ' ./output_data/temp.ps ./output_data/plotspec'//trim(str_inset)//'_thumbs.ps'
          
           end if

          else !regions > 9

           comstr='psnup -l -m25 -n 9 ./output_data/temp.ps '
     &     //'./output_data/plotspec'//trim(str_inset)//'_thumbs.ps'

          end if

          call system(trim(comstr))
          call system('gv ./output_data/plotspec'//trim(str_inset)//'_thumbs.ps &')

!       Remove temporal ps-file
          call system('rm ./output_data/temp.ps')
         
         end if
!       ------------------------------------------------------------------------
        
        END IF !no output
!       ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

!       ------------------------------------------------------------------------
!       Deallocate existing arrays
        call dealloc_arrays
!       ------------------------------------------------------------------------
        end program
